apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'dagger.hilt.android.plugin'
apply plugin: 'kotlin-kapt'

if (getGradle().getStartParameter().getTaskRequests().toString().toLowerCase().contains("gms")) {
    apply plugin: 'com.google.gms.google-services'
}
if (getGradle().getStartParameter().getTaskRequests().toString().toLowerCase().contains("hms")) {
    apply plugin: 'com.huawei.agconnect'
}

android {

    namespace 'com.exponea.example'

    defaultConfig {
        applicationId "com.exponea.example"
        minSdkVersion 21
        targetSdkVersion 35
        compileSdk 35
        versionCode 88
        versionName "4.2.0"
        vectorDrawables.useSupportLibrary = true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    signingConfigs {
        config {
            // Set the parameters based on the actual signing information.
            storeFile file('Exponea.jks')
            storePassword 'Exponea'
            keyPassword 'Exponea'
            keyAlias 'key0'
        }
    }
    buildFeatures {
        viewBinding true
    }
    buildTypes {
        debug {
            signingConfig signingConfigs.config
        }
        release {
            signingConfig signingConfigs.config
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "messagingService"
    productFlavors {
        gms {
            dimension = "messagingService"
        }
        hms {
            dimension = "messagingService"
        }
    }
    kapt {
        correctErrorTypes = true
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.preference:preference-ktx:1.2.0'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.5.3'
    implementation 'androidx.navigation:navigation-ui-ktx:2.5.3'
    implementation 'androidx.hilt:hilt-common:1.2.0'
    implementation 'androidx.work:work-runtime-ktx:2.10.0'
    implementation 'androidx.hilt:hilt-work:1.2.0'
    gmsImplementation 'com.google.firebase:firebase-messaging:23.1.0'
    hmsImplementation 'com.huawei.agconnect:agconnect-core:1.7.3.302'
    hmsImplementation 'com.huawei.hms:push:6.11.0.300'
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation "com.google.dagger:hilt-android:2.51.1"
    kapt "com.google.dagger:hilt-android-compiler:2.51.1"

    implementation project(path: ':sdk')
}
