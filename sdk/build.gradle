apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply from: 'publish-maven.gradle'
apply from: '../jacoco.gradle'
apply plugin: 'com.panaxeo.shadow-aar'
apply plugin: 'com.google.devtools.ksp'
apply plugin: 'androidx.room'

android {

    namespace 'com.exponea.sdk'

    buildFeatures {
        buildConfig = true
        viewBinding true
    }

    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 35
        compileSdk 35
        buildConfigField "String", "EXPONEA_VERSION_NAME", '"4.6.1"'
        buildConfigField "int", "EXPONEA_VERSION_CODE", "88"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'proguard-rules.pro'
        testBuildType "unitTesting"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions { jvmTarget = "17" }

    lintOptions {
        warning "MissingPermission"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            enableAndroidTestCoverage = false
            enableUnitTestCoverage = true
        }
        unitTesting {
            initWith debug
            defaultConfig.minSdkVersion 21
        }
    }

    room {
        schemaDirectory("$projectDir/schemas")
    }

    ksp {
        arg("room.generateKotlin", "true")
    }

    testOptions.unitTests.includeAndroidResources = true
    testOptions {
        unitTests.all {
            jvmArgs '-Xmx2g'
        }
    }

    sourceSets {
        getByName("androidTest").assets.srcDir("$projectDir/schemas")
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    // Database
    implementation("androidx.room:room-runtime:$room_version")
    annotationProcessor("androidx.room:room-compiler:$room_version")

    // To use Kotlin symbol processing tool (ksp)
    ksp("androidx.room:room-compiler:$room_version")

    implementation 'com.squareup.okhttp3:okhttp:4.12.0'
    // Deserialization
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'org.danilopianini:gson-extras:0.2.2'
    // OkHttp Debugger Logger
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.3'
    // Google WorkManager
    implementation "androidx.work:work-runtime-ktx:2.7.1"
    // View for in-app messages
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0'
    // Okhttp HTTP compression interceptor
    implementation 'com.squareup.okhttp3:okhttp-brotli:4.9.1'
    // Html handling for in-app freeform messages
    implementation 'org.jsoup:jsoup:1.15.4'
    // Images handling
    implementationEmbed 'io.coil-kt:coil:1.4.0'
    implementationEmbed 'io.coil-kt:coil-base:1.4.0'
    implementationEmbed 'io.coil-kt:coil-gif:1.4.0'
    implementationEmbed 'io.coil-kt:coil-svg:1.4.0'
    // Custom Tabs
    implementation 'androidx.browser:browser:1.5.0'

    // Base Test
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'androidx.test:core:1.4.0'
    // Kotlin jUnit
    testImplementation 'org.jetbrains.kotlin:kotlin-test-junit:1.2.40'
    // Networking Testing
    testImplementation 'com.squareup.okhttp3:mockwebserver:4.12.0'
    testImplementation 'com.github.gmazzo:okhttp-mock:1.4.1'
    // Improved Unit Testing
    testImplementation "org.robolectric:robolectric:4.14.1"
    // Co-Routines
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.4'
    // Mockk
    testImplementation "io.mockk:mockk:1.12.8"
    testImplementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    // Mockito
    testImplementation 'org.mockito:mockito-core:5.19.0'
    testImplementation 'org.mockito.kotlin:mockito-kotlin:5.4.0'
    // WorkManager testing
    testImplementation 'androidx.work:work-testing:2.4.0'

    // Instrumented tests
    androidTestImplementation 'androidx.test.ext:junit:1.3.0'
    androidTestImplementation 'androidx.test:core:1.7.0'
    androidTestImplementation 'androidx.test:runner:1.7.0'
    androidTestImplementation 'androidx.test:rules:1.7.0'
    // Room testing
    androidTestImplementation "androidx.room:room-testing:$room_version"
}

shadowAar {
    transformR = "com.exponea.sdk.R"
    repackages.put("coil", "freeze.coil")
}

// turn logging for unit tests
tasks.withType(Test) {
    systemProperty "robolectric.logging", "stdout"
}

configurations.all {
    exclude group: "org.jetbrains.kotlin", module: "kotlin-android-extensions-runtime"
}
